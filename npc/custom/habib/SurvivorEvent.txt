-	script	event_rescape::prtevtres	-1,{

// Heure de debut

OnWhisperGlobal:
OnSurvivorStart:
OnClock1800:

announce "[Survivor Event]: A man has been discovered, almost dead, in Main Town. Guards are fighting to keep him alive.",bc_blue|bc_all;
sleep 1000;
announce "[Survivor Event]: He needs help. The three fastest to help him will be rewarded.",bc_blue|bc_all;
sleep 1000;
enablenpc "prtsurv";
enablenpc "prtsurvhyr";
enablenpc "prtsurvmel";
set $@end, 0;

deletearray $checkItem[0], getarraysize($checkItem);
deletearray $checkCount[0], getarraysize($checkCount);

switch(rand(1,3)){
	//Candy Hunt
	case 1: 
		setarray $checkItem[0], 530, 529, 570;
		setarray $checkCount[0], 10, 10, 3;
		break;

	//Herb Hunt
	case 2: 
		setarray $checkItem[0], 510, 631, 511, 509, 621;
		setarray $checkCount[0], 20, 5, 20, 20, 5;	
		break;

	//Bug Hunt
	case 3: 
		setarray $checkItem[0], 1042, 1025, 955, 935;
		setarray $checkCount[0], 10, 5, 15, 15;	
		break;
}
initnpctimer;
end;

OnTimer3000000:
	if ($@end < 3)
	{
		announce "[Survivor Event] To late. The man died from his wounds.",0;
		sleep 1000; 
		announce "[Survivor Event] Next time a wounded man is discovered, help him before he died...",0;
		disablenpc "prtsurv";
		disablenpc "prtsurvhyr";	
		disablenpc "prtsurvmel";
		stopnpctimer;
	}
	end;
}


prontera,155,277,5	script	Survivor::prtsurv	887,{

	set .@npcname$,"^FF3333Survivor^000000";	
	
	mes "[" + .@npcname$ + "]";
	mes "... ^3333FF" + strcharinfo(0) + "^000000 !";
	mes "We've been attacked by the ^3333FF Bloody Pirates ^000000...";
	mes "I'm the only one...I must warn...";
	mes "But I'm so weak...";
	mes "Help me...Bring me what I need to restore,";
	mes "and I'll reward you.";
	next;
	
	switch(select("No problem. Tell me:I got it all:Sorry, no time for that."))
	{
		case 1:
			mes "Here's the list...";
			set SURVIVOR, 0;
			goto L_LIST;
		case 2:
			if ( $@end == 3 )
				goto L_LATE;
			else
				goto L_CHECK;
		case 3:
			goto L_QUIT;
	}
	
	L_LIST:
		for(set .@i,0; .@i<getarraysize( $checkItem ); set .@i,.@i+1)
			mes "^5555FF"+ getelementofarray($checkCount,.@i) +" "+ getitemname(getelementofarray($checkItem,.@i)) +"^000000";
		close;
	
	
	L_QUIT:
		mes "[" + .@npcname$ + "]";
		mes "...Then let me die.";
		close;
	
	L_CHECK:
		mes "[" + .@npcname$ + "]";
		mes "...You came back, ^3333FF" + strcharinfo(0) + "^000000...";
		mes "Do you got it all?";
		next;
		for ( set .@i , 0 ; .@i < getarraysize( $checkItem ) ; set .@i , .@i + 1 )
		{
			if ( countitem( getelementofarray($checkItem,.@i) ) < getelementofarray($checkCount,.@i) )
			{	
				mes "[" + .@npcname$ + "]";
				mes "Sorry, you miss something..." ;
				mes "You gonna let me die?";
				mes "A guess...Want the list again?";
				switch(select("Yes please","No need"))
				{
					case 1:
						goto L_LIST;
					case 2:
						mes "Come back...I'm dying...";
						close;
				}
			}
		}
		if (SURVIVOR == 0)
		{
			goto L_WIN;
		}
		else
			mes "You already got the reward, you can't help me more";
		close;
	
	L_LATE:
		mes "[" + .@npcname$ + "]";
		mes "Too late, sorry.";
		mes "Even if I don't hope to know that again...";
		mes "Maybe another time. I have to warn the guards!";
		close;
	
	L_WIN:
		mes "[" + .@npcname$ + "]";
		mes "Well done!" ;
		mes "Thanks, my friend";
		mes "Here's your reward.";	
		if (.rewardMode == 0)
		{
			logmes "[Survivor] " + strcharinfo(0) + " wins the event and obtains " + .prizeAmt + " Currency Points.";
			callfunc ("AddPoints",getcharid(3), .prizeAmt);
		}
		else
		{
			logmes "[Survivor] " + strcharinfo(0) + " wins the event and obtains " + .prizeAmt + " " + getitemname(.prizeId) + ".";
			getitem .prizeId,.prizeAmt;
		}
		
		mes "I feel much more better.";
		set $@end, ($@end + 1);
		set SURVIVOR, 1;
		announce "[Survivor Event] " + strcharinfo(0) + " wins the event.",0;
		set @check, (3-$@end);
		for ( set .@i , 0 ; .@i < getarraysize( $checkItem ) ; set .@i , .@i + 1 )
		{
			delitem getelementofarray($checkItem,.@i), getelementofarray($checkCount,.@i);
		}
		if (@check > 0)
		{	
			announce "[Survivor Event] Hurry up!",0;
			close;
		}
		else
		{
			announce "[Survivor Event] I'm restored, thanks to all of you.",0;
			disablenpc "prtsurv";
			stopnpctimer;
			close;
		}
		
OnInit:
	set .prizeAmt, 350;
	set .prizeId, 675;
	set .rewardMode, 0;
	disablenpc strnpcinfo(3);
	end;
}
