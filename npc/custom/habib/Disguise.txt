//===== rAthena Script =======================================
//= Disguise Event
//===== By: ==================================================
//= GmOcean
//===== Current Version: =====================================
//= 5.1
//===== Compatible With: =====================================
//= rAthena SVN
//===== Additional Comments: =================================
//= Note: This script requires PCRE to run properly.
//= 5.0 Last update by GmOcean.
//= 5.1 Cleaned and standardized, mostly. [Euphy]
//============================================================

prontera,159,284,4	script	Disguise#0::prtdis	626,{

	// Currently set to run every two hours.
	// To change times, edit the OnClock labels below.

	set .@GMLevel,60;	// GM level required to access NPC.
	set .@npcname$, "[Disguise]";

	if (getgmlevel()>=.@GMLevel) {
		mes .@npcname$;
		mes "Hello!";
		mes "Choose an option.";
		next;
		switch(select("Activate/Deactivate the event:Configuration")) {
		case 1:
			mes .@npcname$;
			if (.EventON) {
				mes "Event status: [^0000FFON^000000]";
				mes "Wanna desactivate it ?";
			} else {
				mes "Event status: [^FF0000OFF^000000]";
				mes "Wanna activate it ?";
			}
			if(select("Yes:No")==2) close;
			if (.EventON) {
				set .EventON,0; set .Timer,0;
				setnpctimer 0; stopnpctimer;
				if (strnpcinfo(2) == "0")
				{
					announce "A GM decided to stop the event. No reward will be given.",bc_map | bc_blue;
				}
				deletepset 1;
				setnpcdisplay strnpcinfo(3),626;
				close;
			}
			set .EventON,1; set .Timer,1; setnpctimer 0; initnpctimer;
			set .ResetCounter,.ResetCounter+1;

			if (strnpcinfo(2) == "0")
			{
				announce "The Disguise event will start in 3 minutes.",bc_all | bc_blue;
				announce "Go near the Reset House (North East) of the Capitale.",bc_all | bc_blue;
			}
			close;
		case 2:
			mes .@npcname$;
			mes "Choose an option.";
			next;
			switch(select("Monsters display:Turn count")) {
			case 1:
				setarray .@r$[0],"All monsters.","Only MVPs.";
				mes .@npcname$;
				mes "Choose the rule.";
				next;
				set .Rule, select(implode(.@r$,":"));
				mes .@npcname$;
				mes "The following rule have been applied:";
				mes "  > ^0055FF"+.@r$[.Rule-1]+"^000000";
				close;
			case 2:
				mes .@npcname$;
				mes "Choose the turn count.";
				mes "Actual count: [^0000FF"+.Rounds+"^000000]";
				next;
				input .@Rounds;
				set .Rounds,.@Rounds;
				mes .@npcname$;
				mes "The turn count have been changed for "+.Rounds+".";
				close;
			case 3:
				mes .@npcname$;
				mes "Input the Item ID of the prize given each round.";
				mes "Current item: [^0000FF"+getitemname(.prize)+"^000000] (ID #"+.prize+")";
				next;
				input .@prize;
				mes .@npcname$;
				if (getitemname(.@prize)=="" || getitemname(.@prize)=="null") {
					mes "That item does not exist. Please try again.";
					close;
				}
				set .prize,.@prize;
				mes "Input the amount to be given.";
				next;
				input .@amount;
				mes .@npcname$;
				if (.@amount<=0 || .@amount>=10000) {
					mes "That amount is invalid. Using default ammount of 1.";
					set .@amount,1;
					next;
					mes .@npcname$;
				}
				set .prizeAmt,.@amount;
				mes "The prize has been changed successfully.";
				mes "prize: "+.prizeAmt+"x [^0000FF"+getitemname(.prize)+"^000000]";
				close;
			}
		}
	}
	if (.EventON) end;
	mes .@npcname$;
	mes "Hello to you, " + strcharinfo(0);
	mes "What can I do for your ?";
	if(select("Informations:Nothing, thanks.")==2) close;
	next;
	mes .@npcname$;
	mes "This event is pretty simple.";
	mes "Each turn, I'm gonna change my appearance, and you should name me.";
	next;
	mes .@npcname$;
	mes "First one get the reward!";
	mes "There is " + .Rounds + " turn per play.";
	close;

OnInit:
	set .EventON,0;
	set .Wait,0;
	set .Winner,0;
	set .ResetCounter,0;
	set .Rounds,10;
	set .prize,675;
	set .prizeAmt,15;
	set .rewardMode,0;
	set .Rule,1;
	setarray .MVP[0],1038,1039,1046,1059,1086,1087,1112,1115,1147,1150,1157,1159,1190,1251,1252,1272,1312,1373,
	                  1389,1399,1418,1492,1502,1511,1583,1623,1630,1646,1647,1648,1649,1650,1651,1658,1685,1688,
				   1708,1719,1734,1751,1768,1779,1785,1802,1832,1871,1874,1885,1917,1980,2022,2068,2087,2131,
				   2156,2165;
	set .BlackList$, "1003,1006,1017,1021,1022,1027,1043,1075,1136,1137,1168," +
	    "1171,1172,1173,1181,1187,1210,1217,1218,1222,1223,1224,1225,1226,1227,1228," +
	    "1233,1284,1407,1411,1414,1495,1501,1900,1996,2000,2001,2002,2003,2004," +
	    "2005,2006,2007,2011,2012,2025,2028,2029,2030,2031,2032,2033,2034,2035," +
	    "2036,2037,2038,2039,2040,2041,2042,2043,2044,2045,2046,2047,2048,2049," +
	    "2050,2051,2052,2053,2054,2055,2056,2057,2058,2059,2060,2061,2062,2063," +
	    "2064,2065,2066,2067,2075,2076,2077,2078,2079,2080,2081,2083,2084,2085," +
	    "2086,2087,2088,2089,2090,2091,2092,2093,2094,2095,2096,2097,2098,2099," +
	    "2100,2101,2012,2103,2104,2105,2106,2107,2108,2109,2110,2111,2112,2113," +
	    "2114,2115,2116,2117,2118,2119,2120,2121,2123,2124,2125,1496,";
	end;

OnWhisperGlobal:
OnStartDisguise:
OnClock1300:
	set .ResetCounter,.ResetCounter+1;
	set .EventON,1;
	set .Timer,1;
	set .Wait,1;
	if (strnpcinfo(2) == "0")
	{
		announce "The Disguise event will start in 3 minutes.",bc_all | bc_blue;
	}
	setnpctimer 0;
	initnpctimer;
	end;
OnTimer10000:
	if (.Timer || .Change) end;
	set .Wait,0;
	goto iDisguise;
	end;
OnTimer30000:
	if (.Timer) end;
	set .Change,0;
	setnpcdisplay strnpcinfo(3),626;
	npctalk "You take too long to answer.";
	sleep 1000;
	npctalk "I was disguised in " + $MonsterName$ + ".";
	sleep 1000;
	npctalk "Wait 10 secondes before next turn.";
	specialeffect EF_DETECT2;
	set $MonsterName$,"";
	deletepset 1;
	stopnpctimer;
	setnpctimer 0;
	initnpctimer;
	end;
OnTimer180000:
	if (.Timer!=1) end;
	if (strnpcinfo(2) == "0")
	{
		announce "The Disguise event started !",bc_all | bc_blue;
	}
	set .Timer,0; stopnpctimer;
	setnpctimer 0; initnpctimer;
iDisguise:
	npctalk "Quel est mon nom?";
	if (.Rule==1) {
		set .Winner,0;
		set .Monster,1000+rand(1,995);
		if (compare(","+.BlackList$+"," , ","+.Monster+",")) goto iDisguise;
		if (.Monster==.LastMonster) goto iDisguise;
		set .LastMonster,.Monster;
		set $MonsterName$,getmonsterinfo(.Monster,0);
	}
	if (.Rule==2) {
		set .Winner,0;
		set .Monster,rand(49);
		set $MonsterName$,getmonsterinfo(.MVP[.Monster],0);
	}
	deletepset 1;
	defpattern 1,"([^:]+):.*"+$MonsterName$+".*", "iCorrect";
	activatepset 1;
	if (.Rule==1) setnpcdisplay strnpcinfo(3),.Monster;
	if (.Rule==2) setnpcdisplay strnpcinfo(3),.MVP[.Monster];
	set .Change,1;
	setnpctimer 0;
	end;
iCorrect:
	if (.Winner) {
		dispbottom "Somebody already got the answer.";
		end;
	}
	set .Winner,1;
	set .RoundCount,.RoundCount+1;
	deletepset 1;
	activatepset 1;
	if (.rewardMode == 0)
	{
		logmes "[Disguise] " + strcharinfo(0) + " wins the event and obtains " + .prizeAmt + " " + getitemname(.prize[.@rand]) + ".";
		callfunc ("AddPoints",getcharid(3), .prizeAmt);
	}
	else
	{
		logmes "[Disguise] " + strcharinfo(0) + " wins the event and obtains " + .prizeAmt + " " + getitemname(.prize) + ".";
		getitem .prize, .prizeAmt;
	}
	npctalk strcharinfo(0)+" found the answer ! I was disguised as : "+$MonsterName$+".";
	if (.RoundCount>=.Rounds) {
		setnpcdisplay strnpcinfo(3),626;
		set .RoundCount,0; set .Change,0; set .EventON,0;
		setnpctimer 0; stopnpctimer;
		sleep 3000;
		npctalk "Thanks everyone for playing. See you next time !";
		end;
	}
	setnpcdisplay strnpcinfo(3),626;
	set .Change,0;
	setnpctimer 0;
	end;
}
