// [========================================================]
// [=============     Evol Soul - Skaldi Quest     ============]
// [========================================================]
// [ Version 1                                              ]
// [========================================================]
// [ Original script by: Alayne				    ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - An evolving weapon system		    	   	    ]
// [ - Contains a quest to unlock skills		    	   	]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v0: Creation                      			    ]
// [                                                        ]
// [========================================================]
// [                                                        ]
// [========================================================]

prontera,183,214,1	script	Researcher Raphael::alaresra	859,{
	set .@npcname$, "[Raphael]";
	mes .@npcname$;
	if (SKADIFAVQ == 1 && RESRAPQ < 3)
	{
		mes "Oh! you came back in one piece. Most of our researchers suffered frost bites when they got close to that being.";
		next;
		mes .@npcname$;
		mes "*Explain everything to Raphael, about the Goddess of Winter and the reason why monsters still appears*";
		next;
		mes .@npcname$;
		mes "I see. That's a very interesting information."; 
		next;
		mes .@npcname$;
		mes "On our way back we found a crystal quiver with an arrow in it. Maybe it belongs to her, you can have it as a reward for all your troubles."; 
		getitem $skadiArrowId, 1;
		set RESRAPQ, 3;
	}
	else if (RESRAPQ == 0)
	{
		mes "Greetings adventurer! I'm Raphael, a researcher from Yuno. I came here to Prontera seeking assistance for my research. Who might you be?";
		if(select("I'm " + strcharinfo(0), "You don't need to know me...") == 1)
		{
			next;
			mes .@npcname$;
			mes "Ah! the famous " + strcharinfo(0) + ".";
			mes "You may find it hard to believe but you're quite popular among the Circle.";
			mes "I do research on Relics and we discovered something strange in snowy place.";
			mes "We'd appreciate your help.";
			if(select("What do you need?", "Not right now, I'm busy.") == 1)
			{
				goto OnAcceptHelping;
			}
			else
			{
				next;
				mes .@npcname$;
				mes "Well, that's a shame. We could've used your help. Let me know if you change your mind.";
				set RESRAPQ, 1;
			}		
		}
		else
		{
			next;
			mes .@npcname$;
			mes "Not much of a talker, I see. Very well then, I won't keep you. Have a good day.";
		}
	}
	else if (RESRAPQ == 1)
	{
		mes "Good to see you again " + strcharinfo(0) + ".";
		mes "Will you be offering your assistance with our research?";
		if(select("I'm listening.", "Not yet.") == 1)
		{
			goto OnAcceptHelping;
		}
		else
		{
			next;
			mes .@npcname$;
			mes "Well, that's a shame. We could've used your help. Let me know if you change your mind.";
		}	
	}
	close;
	
OnAcceptHelping:
	next;
	mes .@npcname$;
	mes "I'm sure that you've noticed that the world became a dangerous place, almost everywhere.";
	mes "It seems that this strange phenomena is caused by a being that appears only during the night.";
	next;
	mes .@npcname$;
	mes "Our issue is that this being doesn't let us approach it.";
	mes "So we would like someone to help us in finding out what secrets this new being has to offer.";
	next;
	mes .@npcname$;
	mes "I can take you to the site where she appears. Would you like to go now?";
	if(select("No time like the present. Let's go.", "Give me some time to prepare myself.") == 1)
	{
		warp "ra_fild01",233,328;
	}
	else
	{
		next;
		mes .@npcname$;
		mes "By all means, but try not to take too long.";
		set RESRAPQ, 2;
	}
	close;
	
OnInit:
	set $skadiArrowId, 30412;
	end;
}

ice_dun03,265,36,1	script	Strange Being::alaresska	971,{
	if (SKADIFAVQ == 2 && STEELSOULCHANGED)
	{
		set .@npcname$, "[Skadi]";
		mes .@npcname$;
		mes "What can I do for you, mortal?";
		if (select("Carve the Words","Frost new Word") == 1)
		{
			next;
			mes .@npcname$;
			mes "Sure. You know that I need some items to carve a Frost Word, don't you?";
			mes "Which word do you want me to carve?";
			set .@firstWord, getequipcardid(EQI_HAND_R,0);
			set .@secondWord, getequipcardid(EQI_HAND_R,1);
			set .@thirdWord, getequipcardid(EQI_HAND_R,2);
			set .@fourhtWord, getequipcardid(EQI_HAND_R,3);
			set .@menu$, "";
			if(.@firstWord != 0)
			{
				set .@menu$, .@menu$ + getitemname(.@firstWord);
			}
			if(.@secondWord != 0)
			{
				set .@menu$, .@menu$ + ":" + getitemname(.@secondWord);
			}
			if(.@thirdWord != 0)
			{
				set .@menu$, .@menu$ + ":" + getitemname(.@thirdWord);
			}
			if(.@fourhtWord != 0)
			{
				set .@menu$, .@menu$ + ":" + getitemname(.@fourhtWord);
			}
			set .@choice, select(.@menu$) - 1;
			next;
			mes .@npcname$;
			set .@maxLevel, 0;
			mes "Allright. To improve this word use, I'll need:";
			if (EVOLVEPATH == 1)
			{
				set .@prefix$, "blessed";
				set .@lvlMaxArrayName$, "$blessedWordGemMaxLvl";
				for(set .@i,0; .@i<getarraysize($blessedWordGem); set .@i,.@i+1)
				{
					if (getequipcardid(EQI_HAND_R,.@choice) == $blessedWordGem[.@i])
					{
						set .@rank, .@i;
						set .@maxLevel, $blessedWordGemMaxLvl[.@i];
					}
				}
			}
			else
			{
				set .@prefix$, "cursed";
				for(set .@i,0; .@i<getarraysize($cursedWordGem); set .@i,.@i+1)
				{
					if (getequipcardid(EQI_HAND_R,.@choice) == $cursedWordGem[.@i])
					{
						set .@rank, .@i;
						set .@maxLevel, $cursedWordGemMaxLvl[.@i];
					}
				}
			}
			explode(.@requirement$,getd("." + .@prefix$ + "Skill" + (.@rank + 1) + "$"),",");
			if (getarraysize(.@requirement$) > 0)
			{
				set .@initialLevel, getd("wordGem" + .@choice + "Lvl");
				set .@menuItem$, "";
				for(set .@i,0; .@i<getarraysize(.@requirement$); set .@i,.@i+2)
				{
					mes "	~ " + (atoi(.@requirement$[.@i + 1])  * (.@initialLevel + 1)) + " " + getitemname(atoi(.@requirement$[.@i]));
					set .@menuItem$, .@menuItem$ + (.@i ? ":" : "") + getitemname(atoi(.@requirement$[.@i]));
				}
				next;
				mes .@npcname$;
				mes "So. Are you ready?";
				if(select("Yes","No, not yet") == 1)
				{
					next;
					mes .@npcname$;
					mes "Allright, let's see that.";
					if (.@initialLevel >= .@maxLevel)
					{
						next;
						mes .@npcname$;
						mes "Sorry, but this Power Word is already at max level.";
						close;
					}
					set .@success, 1;
					for(set .@i,0; .@i<getarraysize(.@requirement$) && .@success == 1; set .@i,.@i+2)
					{
						set .@variableName$, "requirement" + .@choice + "_" + .@i;
						if(getd(.@variableName$) + countitem(atoi(.@requirement$[.@i])) < (atoi(.@requirement$[.@i + 1]) * (.@initialLevel + 1)))
						{
							set .@missingItem$, getitemname(atoi(.@requirement$[.@i]));
							set .@missingCount, (atoi(.@requirement$[.@i + 1]) * (.@initialLevel + 1)) - (getd(.@variableName$) + countitem(atoi(.@requirement$[.@i])));
							set .@success, 0;
						}
					}
					next;
					mes .@npcname$;
					if (.@success == 0)
					{
						mes "I'm sorry, but you're still lacking some " + .@missingItem$ + ". You'll need " + .@missingCount + " more.";
						close;
					}
					for(set .@i,0; .@i<getarraysize(.@requirement$); set .@i,.@i+2)
					{
						set .@variableName$, "requirement" + .@choice + "_" + .@i;
						delitem atoi(.@requirement$[.@i]), (atoi(.@requirement$[.@i + 1]) - getd(.@variableName$));
						setd(.@variableName$,0);
					}
					setd("wordGem" + (.@choice + 1) + "Lvl", (.@initialLevel + 1));
					next;
					mes .@npcname$;
					unequip EQI_HAND_R;
					specialeffect2 883; //epiclesis
					mes "Here you are. You're Relic has been improved, by receiving the approbation of Yggdrasil.";
					mes "See you soon, mortal.";
				}
				else
				{
					next;
					mes .@npcname$;
					mes "I understand.";
					mes "Maybe you wish me to keep some items you already gathered?";
					if (select("Yes, thanks","No, no need") == 1)
					{
						next;
						mes .@npcname$;
						mes "Sure. Which item?";
						set .@item, select(.@menuItem$) - 1;
						next;
						mes .@npcname$;
						mes "How many? '0' means all those you have.";
						input .@count;
						if (.@count == 0)
						{
							set .@count, countitem(atoi(.@requirement$[.@item * 2]));
						}
						if (.@count <= countitem(atoi(.@requirement$[.@item * 2])))
						{
							next;
							mes .@npcname$;
							mes "Perfect. I'll keep them for you then.";
							set .@variableName$, "requirement" + .@choice + "_" + .@item;
							setd(.@variableName$,getd(.@variableName$) + .@count);
							delitem atoi(.@requirement$[.@item * 2]), .@count;
						}
						else
						{
							next;
							mes .@npcname$;
							mes "You don't have that much items...";
						}
					}
					else
					{
						next;
						mes .@npcname$;
						mes "As you wish.";
					}
				}
			}
			else
			{
				next;
				mes .@npcname$;
				mes "Sorry, I'm a bit tired...";
				mes "*An error occured, please contact a gm.";
			}
		}
		else
		{
			mes "Did you find some Words?";
			setarray .@foundScrolls[0], 0;
			set .@countScrolls, 0;
			if(EVOLVEPATH == 1)
			{
				copyarray $wordScroll[0], $blessedWordScrolls[0], getarraysize($blessedWordScrolls);
			}
			else if (EVOLVEPATH == 2)
			{
				copyarray $wordScroll[0], $cursedWordScrolls[0], getarraysize($cursedWordScrolls);
			}
			for(set .@i,0; .@i<getarraysize($wordScroll); set .@i,.@i+1)
			{
				if (countitem($wordScroll[.@i]) > 0)
				{
					set .@foundScrolls[.@countScrolls], $wordScroll[.@i];
					set .@countScrolls, .@countScrolls + 1;
				}
			}
			if (.@countScrolls > 0 && STEELSOULID == getequipid(EQI_HAND_R))
			{
				next;
				mes .@npcname$;
				mes "Hum...Looks like.";
				mes "Not to bad, for a mortal.";
				mes "What do you like to do?";
				mes "Which Word do you wanna insert as Power Stone in your Relic?";
				set .@menu$, "";
				for(set .@i,0; .@i<getarraysize(.@foundScrolls); set .@i,.@i+1)
				{
					set .@menu$, .@menu$ + (.@i ? ":" : "") + getitemname(.@foundScrolls[.@i]);
				}
				set .@choice, select(.@menu$) - 1;
				if (countitem(.@foundScrolls[.@choice]) > 0)
				{
					//retrieve compounded cards
					set .@cardId1, getequipcardid(EQI_HAND_R,0);
					set .@cardId2, getequipcardid(EQI_HAND_R,1);
					set .@cardId3, getequipcardid(EQI_HAND_R,2);
					set .@cardId4, getequipcardid(EQI_HAND_R,3);
					
					set .@gemId, .@foundScrolls[.@choice] + 60; //there's a difference of 60 slots between scroll and shard
					
					set .@cardRank, -1;
					//coumpound in first free slot
					if (.@cardId1 == 0)
					{
						set .@cardId1, .@gemId;	
						set wordGem1Lvl, 1;
					}
					else if (.@cardId2 == 0)
					{
						set .@cardId2, .@gemId;	
						set wordGem2Lvl, 1;
					}
					else if (.@cardId3 == 0)
					{
						set .@cardId3, .@gemId;	
						set wordGem3Lvl, 1;
					}
					else if (.@cardId4 == 0)
					{
						set .@cardId4, .@gemId;	
						set wordGem4Lvl, 1;					
					}
					else
					{
						next;
						mes .@npcname$;
						mes "You've already compounded multiple gems. I can't do more.";
						close;
					}
					
					set .@refine, getequiprefinerycnt(EQI_HAND_R);
					
					next;
					mes .@npcname$;
					mes "Perfect. Wait a second.";
					delequip EQI_HAND_R;
					getitem2 STEELSOULID, 1, 1, .@refine, 0, .@cardId1, .@cardId2, .@cardId3, .@cardId4;
					equip STEELSOULID;
					delitem .@foundScrolls[.@choice], 1;
				}
				else
				{
					next;
					mes .@npcname$;
					mes "You shouldn't try to trap a Goddess...";
					percentheal -50, -50;
					warp "Save", 0, 0;
				}
			}
		}
		set LASTSKADILVL, STEELLVL;
	}
	else if (SKADIFAVQ == 1 && countitem($skadiArrowId) > 0)
	{
		set .@npcname$, "[Skadi]";
		mes .@npcname$;
		mes "You've found one of my special arrows. You have my gratitude, mortal.";
		mes "From now, I'll have to explain you a bit more about my power.";
		next;
		mes .@npcname$;
		mes "The Cold I can produce is not the cold you may feel while in the middle of a snow storm.";
		mes "It's wide deeper than that.";
		mes "I can freeze anything.";
		next;
		mes .@npcname$;
		mes "And that include the power of Original Words.";
		mes "Among the world, Ases spread ancient words of power.";
		mes "If you know them, I can use my powers to frost them, and allow you to use the power which is hidden inside the words.";
		mes "You'll find those words, randomly, by killing monsters.";
		mes "Don't read them, no matter what! You're soul won't be strong enough to read them before I frost them.";
		next;
		mes .@npcname$;
		mes "Once frost, you'll be able to insert it as gems inside a specific weapon.";
		mes "It has to be a Relic, a proof that your soul is strong enough to endure the inside power it contains.";
		mes "Then, you'll be able to carve the rune created by the Cold, to increase the use you'll be able to made of these words.";
		mes "But each carve will need a paiement.";
		mes "The highest the carve, the highest the price.";
		mes "Be warned.";
		delitem $skadiArrowId, 1;
		set SKADIFAVQ, 2;
	}
	else
	{
		if (SKADIQ == 0)
		{
			set .@npcname$, "[Strange Being]";
			mes .@npcname$;
			mes "*She keeps muttering something*";
			mes "Sleep I could not";
			mes "on the sea beds";
			mes "for the screeching of the bird.";
			mes "That gull wakes me";
			mes "when from the wide sea";
			mes "he comes each morning";
			next;
			mes .@npcname$;
			mes "*Her glance makes you feel colder*";
			mes "What do you seek mortal?";
			mes "Why do you disturb my solitude with your presence? ";
			if(select("I'd like to know who you are.", "I'm sorry, I was just leaving.") == 1)
			{
				next;
				mes .@npcname$;
				mes "You're a brave one, unlike the ones that appeared before you.";
				mes "Only one without selfish desires can withstand my cold aura.";
				mes "They call me Skadi, the Goddess of Winter.";
				next;
				set .@npcname$, "[Skadi]";
				mes .@npcname$;
				mes "This is just a manifestation in flesh and not my true form.";
				mes "Now, tell me your name and what brings you so far in the north to this place.";
				if(select("Tell her your name and ask about the monsters.", "Lie to her and try to learn more.") == 1)
				{
					next;
					mes .@npcname$;
					mes "" + strcharinfo(0) + ", the monsters is my doing. I loathe the darkness and all that dwells within.";
					mes "The Draugr have stolen my arrows and without them, I can hunt no more.";
					mes "Perhaps you may be able to help me.";
					if(select("What do I have to do?", "I'm outta here.") == 1)
					{
						next;
						mes .@npcname$;
						mes "Don't be so hasty young one. First, I'll need to test your might to see if you are capable of handling such a task.";
						next;
						mes .@npcname$;
						mes "Do you see these creatures of Ice in this place? Slay an hundred and come see me when you are done. If you cannot handle such a task then you won't be of much help to me.";
						set SKADIQ, 1;
					}
				}
				else
				{
					next;
					mes .@npcname$;
					mes "I sense malice in you. Begone foul creature.";
					skill 14,35,0;
				}
			}
			else
			{
				next;
				mes .@npcname$;
				mes "Such fragile creature. Leave if you must and do not return to this place again for your own good.";
			}
		}
		else if (SKADIQ == 1)
		{
			set .@npcname$, "[Skadi]";
			mes .@npcname$;		
			if (SKADIFAVKILL < $skadiKillNeed)
			{
				mes "You've managed to slay " + SKADIFAVKILL + " creature of Ice. Remember that you need to slay " + $skadiKillNeed + ".";
			}
			else
			{
				mes "Impressive for a mortal to achieve such feat in such a short time.";
				next;
				mes .@npcname$;
				mes "I'll be able to hunt again when all my arrows will be retrieved.";
				mes "If you can help me once more, I'll be sure to reward you.";
				set SKADIQ, 2;
				set SKADIFAVQ, 1;
			}
		}
	}
	close;
	
OnNPCKillEvent:
	if(SKADIQ == 1 && killedrid == $draurgId)
	{
		SKADIFAVKILL += 1;
		if(SKADIFAVKILL >= skadiKillNeed)
		{
			dispbottom "[Skadi] I can feel it...You're ready.";
		}
	}
	end;

OnWhisperGlobal:
	debugmes "Steel soul: " + getitemname(STEELSOULID);
	setd("wordGem1Lvl", 2);
	debugmes "Gem 1 lvl: " + getd("wordGem1Lvl");
	debugmes "Gem 2 lvl: " + getd("wordGem2Lvl");
	debugmes "Gem 3 lvl: " + getd("wordGem3Lvl");
	debugmes "Gem 4 lvl: " + getd("wordGem4Lvl");
	end;
	
OnInit:
	set $skadiKillNeed, 10;
	set $draurgId, 1777;

	setarray $blessedWordScrolls[0], 30265,30266,30267,30268,30269,30270,30271,30272,30273,30274,30275,30276,30277,30278,30279,30280,30281,30282,30283,
	30284,30285,30286,30287,30288,30289,30290,30291,30292,30293,30294;
	setarray $cursedWordScrolls[0], 30295,30296,30297,30298,30299,30300,30301,30302,30303,30304,30305,30306,30307,
	30308,30309,30310,30311,30312,30313,30314,30315,30316,30317,30318,30319,30320,30321,30322,30323,30324;
	setarray $blessedWordGem[0], 30325,30326,30327,30328,30329,30330,30331,30332,30333,30334,30335,30336,30337,30338,30339,30340,30341,30342,30343,
	30344,30345,30346,30347,30348,30349,30350,30351,30352,30353,30354;
	setarray $blessedWordGemMaxLvl[0], 5,5,1,10,5,5,5,5,5,5,5,5,10,1,5,5,5,5,5,5,10,10,5,5,5,5,5,5,5,5;
	setarray $cursedWordGem[0],30355,30356,30357,30358,30359,30360,30361,30362,30363,30364,30365,30366,30367,30368,
	30369,30370,30372,30373,30374,30375,30376,30377,30378,30379,30380,30381,30382,30383,30384;
	setarray $cursedWordGemMaxLvl[0], 5,10,5,10,3,5,5,5,5,1,5,5,5,5,5,5,5,5,5,5,5,5,5,5,5,5,5,10,5,5;
	//RK
	//Enchant Blade
	setarray .blessedSkill1$, "7106,250,7446,100,7072,50,615,1"; //pattern: id, count
	//Sonic Wave
	setarray .blessedSkill2$, "7107,150,7003,250,908,150,615,1"; //pattern: id, count
	//Death Bound
	setarray .blessedSkill3$, "1035,350,746,100,911,100,615,1"; //pattern: id, count
	//Hundred Spear
	setarray .blessedSkill4$, "7047,150,7512,50,924,250,615,1"; //pattern: id, count
	//Ignition Break
	setarray .blessedSkill5$, "1042,50,1097,150,946,150,615,1"; //pattern: id, count
	//GC
	//Cross Impact
	setarray .blessedSkill6$, "930,250,7448,100,937,100,615,1"; //pattern: id, count
	//Hallucination Walk
	setarray .blessedSkill7$, "1036,350,7445,50,955,100,615,1"; //pattern: id, count
	//Dark Illusion
	setarray .blessedSkill8$, "943,150,967,250,910,150,615,1"; //pattern: id, count
	//Weapon Crush
	setarray .blessedSkill9$, "7563,250,7447,50,935,150,615,1"; //pattern: id, count
	//Cloaking Exceed
	setarray .blessedSkill10$, "1045,150,948,250,938,200,615,1"; //pattern: id, count
	//AB
	//Offertorium
	setarray .blessedSkill11$, "7207,100,1096,250,909,100,615,1"; //pattern: id, count
	//Clearance
	setarray .blessedSkill12$, "7507,250,1028,150,713,150,615,1"; //pattern: id, count
	//Duple Light
	setarray .blessedSkill13$, "7262,150,7043,100,1052,200,615,1"; //pattern: id, count
	//Renovatio
	setarray .blessedSkill14$, "1095,250,7124,100,919,150,615,1"; //pattern: id, count
	//Expiatio
	setarray .blessedSkill15$, "1037,150,7157,50,921,200,615,1"; //pattern: id, count
	//WL
	//Crimson Rock
	setarray .blessedSkill16$, "7123,150,7156,100,1027,200,615,1"; //pattern: id, count
	//Jack Frost
	setarray .blessedSkill17$, "7319,250,7066,100,914,100,615,1"; //pattern: id, count
	//Chain Lightning
	setarray .blessedSkill18$, "7064,250,963,150,1026,100,615,1"; //pattern: id, count
	//Earth Strain
	setarray .blessedSkill19$, "7209,150,1015,100,917,200,615,1"; //pattern: id, count
	//Marsh of Abyss
	setarray .blessedSkill20$, "7099,100,7110,150,961,150,615,1"; //pattern: id, count
	//RA
	//Arrow Storm
	setarray .blessedSkill21$, "7151,250,7121,150,964,100,615,1"; //pattern: id, count
	//Aimed Bolt
	setarray .blessedSkill22$, "912,200,1039,100,924,150,615,1"; //pattern: id, count
	//Electric Shocker
	setarray .blessedSkill23$, "7115,250,1038,150,7006,100,615,1"; //pattern: id, count
	//Fear Breeze
	setarray .blessedSkill24$, "7125,150,1048,250,913,100,615,1"; //pattern: id, count
	//Icebound Trap
	setarray .blessedSkill25$, "7068,100,7219,150,1057,250,615,1"; //pattern: id, count
	//MC
	//Axe Boomerang
	setarray .blessedSkill26$, "904,150,941,250,953,100,615,1"; //pattern: id, count
	//Training Axe
	setarray .blessedSkill27$, "7116,150,7214,150,947,150,615,1"; //pattern: id, count
	//Power Swing
	setarray .blessedSkill28$, "7120,250,7096,100,962,150,615,1"; //pattern: id, count
	//Axe Tornado
	setarray .blessedSkill29$, "7561,250,7053,150,705,100,615,1"; //pattern: id, count
	//Magic Decoy
	setarray .blessedSkill30$, "7097,150,932,250,905,100,615,1"; //pattern: id, count
	
	//SC
	//Shadow Form
	setarray .cursedSkill1$, "7106,250,7446,100,7072,50,615,1"; //pattern: id, count
	//Triangle Shot
	setarray .cursedSkill2$, "7107,150,7003,250,908,150,615,1"; //pattern: id, count
	//Body Paint
	setarray .cursedSkill3$, "1035,350,746,100,911,100,615,1"; //pattern: id, count
	//Reproduce
	setarray .cursedSkill4$, "7047,150,7512,50,924,250,615,1"; //pattern: id, count
	//Dimensionnal Door
	setarray .cursedSkill5$, "1042,50,1097,150,946,150,615,1"; //pattern: id, count
	//LG
	//Vanguard Force
	setarray .cursedSkill6$, "930,250,7448,100,937,100,615,1"; //pattern: id, count
	//Cannon Spear
	setarray .cursedSkill7$, "1036,350,7445,50,955,100,615,1"; //pattern: id, count
	//Pinpoint
	setarray .cursedSkill8$, "943,150,967,250,910,150,615,1"; //pattern: id, count
	//Prestige
	setarray .cursedSkill9$, "7563,250,7447,50,935,150,615,1"; //pattern: id, count
	//Burst Attack
	setarray .cursedSkill10$, "1045,150,948,250,938,200,615,1"; //pattern: id, count
	//SR
	//Lightning Walk
	setarray .cursedSkill11$, "7207,100,1096,250,909,100,615,1"; //pattern: id, count
	//Earth Shaker
	setarray .cursedSkill12$, "7507,250,1028,150,713,150,615,1"; //pattern: id, count
	//Gentle Touch - Energy Gain
	setarray .cursedSkill13$, "7262,150,7043,100,1052,200,615,1"; //pattern: id, count
	//Rampage Blaster
	setarray .cursedSkill14$, "1095,250,7124,100,919,150,615,1"; //pattern: id, count
	//Gentle Touch - Cure
	setarray .cursedSkill15$, "1037,150,7157,50,921,200,615,1"; //pattern: id, count
	//WM
	//Harmonize
	setarray .cursedSkill16$, "7123,150,7156,100,1027,200,615,1"; //pattern: id, count
	//Metalic Sound
	setarray .cursedSkill17$, "7319,250,7066,100,914,100,615,1"; //pattern: id, count
	//Death Valley
	setarray .cursedSkill18$, "7064,250,963,150,1026,100,615,1"; //pattern: id, count
	//Gloomy Shyness
	setarray .cursedSkill19$, "7209,150,1015,100,917,200,615,1"; //pattern: id, count
	//Despair Song
	setarray .cursedSkill20$, "7099,100,7110,150,961,150,615,1"; //pattern: id, count
	//SO
	//Striking
	setarray .cursedSkill21$, "7151,250,7121,150,964,100,615,1"; //pattern: id, count
	//Warmer
	setarray .cursedSkill22$, "912,200,1039,100,924,150,615,1"; //pattern: id, count
	//Killing Cloud
	setarray .cursedSkill23$, "7115,250,1038,150,7006,100,615,1"; //pattern: id, count
	//Fire Walk
	setarray .cursedSkill24$, "7125,150,1048,250,913,100,615,1"; //pattern: id, count
	//Varetyr Spear
	setarray .cursedSkill25$, "7068,100,7219,150,1057,250,615,1"; //pattern: id, count
	//GN
	//Blood Sucker
	setarray .cursedSkill26$, "904,150,941,250,953,100,615,1"; //pattern: id, count
	//Demonic Fire
	setarray .cursedSkill27$, "7116,150,7214,150,947,150,615,1"; //pattern: id, count
	//Crazy Vines
	setarray .cursedSkill28$, "7120,250,7096,100,962,150,615,1"; //pattern: id, count
	//Thorn Trap
	setarray .cursedSkill29$, "7561,250,7053,150,705,100,615,1"; //pattern: id, count
	//Hell's Plant
	setarray .cursedSkill30$, "7097,150,932,250,905,100,615,1"; //pattern: id, count

	set $reliclootMaxRate, 100;
	set $requirementItemLoot, 25;
	setarray $requiredSkillsItems[0],0;
	end;
}